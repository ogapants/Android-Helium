repositories {
    jcenter()
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.gfx:android-power-assert-plugin:1.1.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.gfx.ribbonizer:plugin:0.5.0'
    }
}

apply plugin: 'com.android.application'
//apply plugin: 'android-power-assert'
apply plugin: 'com.neenbedankt.android-apt'
//apply plugin: 'com.github.gfx.ribbonizer'

apply from: 'https://raw.githubusercontent.com/gfx/gradle-android-utils/master/build.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    dataBinding.enabled = true
    defaultConfig {
        applicationId "com.github.gfx.helium"
        minSdkVersion 16
        targetSdkVersion 23
        def version = readVersion(rootProject.file("VERSION"))
        versionName version.toVersionName()
        versionCode version.toVersionCode()
        testInstrumentationRunner 'org.robolectric.RobolectricGradleTestRunner'
        resConfigs "ja"
    }
    signingConfigs {
        release {
            if (project.ext.has('ReleaseStoreFile')) {
                storeFile file(project.ext.ReleaseStoreFile.replaceAll("~", System.getenv("HOME")))
                storePassword project.ext.ReleaseStorePassword
                keyAlias project.ext.ReleaseKeyAlias
                keyPassword project.ext.ReleaseKeyPassword
            }
        }
    }
    buildTypes {
        release {
            shrinkResources true

            if (signingConfigs.release.storeFile != null &&
                    signingConfigs.release.storeFile.exists()) {
                signingConfig signingConfigs.release
            } else {
                logger.info(
                        "keystore ${signingConfigs.release.storeFile} does not exist. Release signing is ignored.")
            }

            buildConfigField("String", "GA_TRACKING_ID",
                    "\"${project.ext.has('GA_TRACKING_ID_RELEASE') ? project.ext.GA_TRACKING_ID_RELEASE : ''}\"")
        }

        debug {
            shrinkResources true

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            buildConfigField("String", "GA_TRACKING_ID",
                    "\"${project.ext.has('GA_TRACKING_ID_DEBUG') ? project.ext.GA_TRACKING_ID_DEBUG : ''}\"")
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.google.android.gms:play-services-ads:8.3.0'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.cookpad.android.rxt4a:rxt4a:1.0.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile('com.squareup.retrofit:converter-simplexml:1.9.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.github.gfx.android.orma:orma-processor:0.14.0'
    compile 'com.github.gfx.android.orma:orma:0.14.0'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.2.0'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.google.guava:guava:18.0'
}
